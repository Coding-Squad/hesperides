#!groovy

@Library(["github"]) _

import groovy.json.JsonSlurperClassic

pipeline {
    agent {
        node {
            label 'maven3'
        }
    }
    options {
        buildDiscarder(logRotator(numToKeepStr: '10'))
    }
    parameters {
        string(name: 'RELEASE_VERSION', description: 'Release Version')
    }
    stages {
        stage('Validate parameters') {
            when {
                expression { params.RELEASE_VERSION.startsWith('v') }
            }
            steps {
                error("RELEASE_VERSION must not start with a v !")
            }
        }
        stage("Pull Project") {
            steps {
                script {
                    git(url: 'https://github.com/voyages-sncf-technologies/hesperides.git',
                        credentialsId: 'github-hesperides-adm',
                        branch: 'develop')
                }
            }
        }
        stage("Create release branch") {
            steps {
                script {
                    withMaven(mavenSettingsConfig: 'global_maven_settings') {
                        sh "mvn help:effective-settings"
                        sh "mvn -B gitflow:release-start -DreleaseVersion=${params.RELEASE_VERSION}"
                    }
                    withCredentials([usernamePassword(credentialsId: 'github-hesperides-adm', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
                        sh "git config user.name \"hesperides-adm\""
                        sh "git push https://${USERNAME}:${PASSWORD}@github.com/voyages-sncf-technologies/hesperides.git release/${params.RELEASE_VERSION}"
                    }
                }
            }
        }
        stage('Create tag') {
            steps {
                script {
                    withCredentials([string(credentialsId: 'github-hesperides-adm-token', variable: 'TOKEN_ID')]) {
                        github.authenticate organization: 'voyages-sncf-technologies', project: 'hesperides', token: "${TOKEN_ID}"
                        github.createRelease tag: "v${params.RELEASE_VERSION}", branch: "release/${params.RELEASE_VERSION}", description: "${params.RELEASE_VERSION}", draft: false, prerelease: false
                    }
                }
            }
        }
        stage('Merge and push release on develop and master') {
            steps {
                script {
                    withCredentials([usernamePassword(credentialsId: 'github-hesperides-adm', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
                        sh "git config user.name \"hesperides-adm\""
                        sh "git checkout develop"
                        sh "git merge release/${params.RELEASE_VERSION}"
                        sh "git push https://${USERNAME}:${PASSWORD}@github.com/voyages-sncf-technologies/hesperides.git"
                        sh "git checkout master"
                        sh "git merge release/${params.RELEASE_VERSION}"
                        sh "git push https://${USERNAME}:${PASSWORD}@github.com/voyages-sncf-technologies/hesperides.git"
                    }
                }
            }
        }
        stage('Trigger teeproxy image generation for this release') {
            steps {
                build job: "../teeproxy/build_docker_image", parameters: [[$class: 'StringParameterValue', name: 'DOCKER_TAG', value: "v${params.RELEASE_VERSION}"]]
            }
        }
        stage('Trigger hesperides-data-migration image generation for this release') {
            steps {
                build job: "../data-migration/build_docker_image", parameters: [[$class: 'StringParameterValue', name: 'DOCKER_TAG', value: "v${params.RELEASE_VERSION}"]]
            }
        }
        stage('Trigger hesperides image generation for this release') {
            steps {
                timeout(time: 900, unit: 'SECONDS') {
                    waitUntil {   // We wait for Travis CI to build the Docker image with the new tag
                        sleep 10  // to avoid hitting Docker hub API too frequently
                        script {
                            def tags = new JsonSlurperClassic().parseText(httpRequest(httpProxy: env.http_proxy, url: 'https://registry.hub.docker.com/v1/repositories/hesperides/hesperides/tags').content)
                            return tags.any { it.name == "v${params.RELEASE_VERSION}" }
                        }
                    }
                }
                build job: "build_docker_image", parameters: [[$class: 'StringParameterValue', name: 'DOCKER_TAG', value: "v${params.RELEASE_VERSION}"]]
            }
        }
    }

    post {
        always {
            deleteDir()
        }
    }
}
